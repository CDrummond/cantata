if (FFMPEG_FOUND OR MPG123_FOUND)
    include_directories(${QTINCLUDES} ${ZLIB_INCLUDE_DIRS})
    if (FFMPEG_FOUND)
        set(CANTATA_RG_SRCS ${CANTATA_RG_SRCS} ffmpeginput.cpp)
        include_directories(${FFMPEG_INCLUDE_DIRS})
    endif (FFMPEG_FOUND)
    if (MPG123_FOUND)
        set(CANTATA_RG_SRCS ${CANTATA_RG_SRCS} mpg123input.cpp)
        include_directories(${MPG123_INCLUDE_DIRS})
    endif (MPG123_FOUND)

    include_directories(${CMAKE_SOURCE_DIR}/replaygain ${CMAKE_SOURCE_DIR}/support)
    set(CANTATA_RG_SRCS ${CANTATA_RG_SRCS} main.cpp replaygain.cpp trackscanner.cpp jobcontroller.cpp ../support/thread.cpp)
    set(CANTATA_RG_MOC_HDRS ${CANTATA_RG_MOC_HDRS} replaygain.h trackscanner.h jobcontroller.h ../support/thread.h)

    if (ENABLE_KDE_SUPPORT)
        kde4_add_executable(cantata-replaygain ${CANTATA_RG_SRCS} ${CANTATA_RG_MOC_SRCS})
        install(TARGETS cantata-replaygain RUNTIME DESTINATION lib/cantata)
    else (ENABLE_KDE_SUPPORT)
        if (ENABLE_QT5)
            QT5_WRAP_CPP(CANTATA_RG_MOC_SRCS ${CANTATA_RG_MOC_HDRS})
        else (ENABLE_QT5)
            INCLUDE(${QT_USE_FILE})
            QT4_WRAP_CPP(CANTATA_RG_MOC_SRCS ${CANTATA_RG_MOC_HDRS})
        endif (ENABLE_QT5)

        if (WIN32)
            set(CMAKE_BUILD_TYPE "Release")
            ADD_EXECUTABLE(cantata-replaygain WIN32 ${CANTATA_RG_SRCS} ${CANTATA_RG_MOC_SRCS} ${CANTATA_PO})
            install(TARGETS cantata-replaygain DESTINATION ${CMAKE_INSTALL_PREFIX}/helpers)
        else (WIN32)
            ADD_EXECUTABLE(cantata-replaygain ${CANTATA_RG_SRCS} ${CANTATA_RG_MOC_SRCS})
            install(TARGETS cantata-replaygain RUNTIME DESTINATION lib/cantata)
        endif (WIN32)
    endif (ENABLE_KDE_SUPPORT)

    find_package(Ebur128)

    if (EBUR128_FOUND)
        message("-- Using system libebur128")
        include_directories(${EBUR128_INCLUDE_DIR})
        target_link_libraries(cantata-replaygain ${EBUR128_LIBRARY})
    else (EBUR128_FOUND)
        message("-- Using supplied libebur128")
        include_directories(${CMAKE_SOURCE_DIR}/3rdparty/ebur128)
        add_subdirectory(${CMAKE_SOURCE_DIR}/3rdparty/ebur128 ${CMAKE_BINARY_DIR}/3rdparty/ebur128)
        target_link_libraries(cantata-replaygain ebur128)
    endif (EBUR128_FOUND)

    if (FFMPEG_FOUND)
        target_link_libraries(cantata-replaygain ${FFMPEG_LIBRARIES})
    endif (FFMPEG_FOUND)
    if (MPG123_FOUND)
        target_link_libraries(cantata-replaygain ${MPG123_LIBRARIES})
    endif (MPG123_FOUND)
    target_link_libraries(cantata-replaygain ${QTCORELIBS})
    if (UNIX AND NOT APPLE)
        target_link_libraries(cantata-replaygain -lpthread)
    endif (UNIX AND NOT APPLE)
endif (FFMPEG_FOUND OR MPG123_FOUND)

